
-------------------------------------------------------------------------------
-- Copyright 2020.
-- CSIRO.
-- All Rights Reserved.
--  
--
--   File Name:      adc_spi_wrapper_v1_0.vgen
--   Type:           VGEN
--   Designer:       J. Tuthill
--   Created:        Sun Apr 26 11:10:28 2020
--   Template Rev:   2.0
--
--   Title:          Vector generator for ADC SPI top-level wrapper.
--   Description: 
--   
--   
--   
--
--   
--   The following is updated by RCS/CVS.
--   Modification History:
--   $Log$
--   
--   
-------------------------------------------------------------------------------

-------------------------------------------------------------------------------
--  The following line is used by the testbench generator and the
--  av-simulate function. 
--  NOTE: The time can be changed from ns to; ps,ns,us,ms.
--              TOTAL SIMULATION TIME (ns): 100 
--                 generate_simulus_files = true
--                 generate_capture_files = true
--                   dont_touch_tben_file = false
--                    dont_touch_cmd_file = false
--  TRACE SIGNALS
--  /ADC_SPI_WRAPPER_V1_0_TBEN/DUT/*
--
-------------------------------------------------------------------------------


library ieee;
use ieee.std_logic_1164.all;
use ieee.numeric_std.all;


-- Add user defined use clauses under here



-------------------------------------------------------------------------------
entity adc_spi_wrapper_v1_0_vgen is

   port (
      spi0_sclk_i     : in     std_logic;
      spi0_sclk_o     : buffer std_logic;
      spi0_mosi_i     : in     std_logic;
      spi0_mosi_o_dly : buffer std_logic;
      spi0_miso_i     : in     std_logic;
      spi0_ss_i       : in     std_logic;
      spi0_ss_o_dly   : buffer std_logic;
      spi_sclk        : in     std_logic;
      spi_mosi        : in     std_logic;
      spi_miso_dly    : buffer std_logic;
      spi_ss          : in     std_logic;
      adc_drdy_n_dly  : buffer std_logic;

      -- Ports of Axi Slave Bus Interface S00_AXI -- register interface --
      s00_axi_aclk        : buffer std_logic;
      s00_axi_aresetn_dly : buffer std_logic;
      s00_axi_awaddr_dly  : buffer std_logic_vector(5 downto 0);
      s00_axi_awprot_dly  : buffer std_logic_vector(2 downto 0);
      s00_axi_awvalid_dly : buffer std_logic;
      s00_axi_awready     : in     std_logic;
      s00_axi_wdata_dly   : buffer std_logic_vector(31 downto 0);
      s00_axi_wstrb_dly   : buffer std_logic_vector(3 downto 0);
      s00_axi_wvalid_dly  : buffer std_logic;
      s00_axi_wready      : in     std_logic;
      s00_axi_bresp       : in     std_logic_vector(1 downto 0);
      s00_axi_bvalid      : in     std_logic;
      s00_axi_bready_dly  : buffer std_logic;
      s00_axi_araddr_dly  : buffer std_logic_vector(5 downto 0);
      s00_axi_arprot_dly  : buffer std_logic_vector(2 downto 0);
      s00_axi_arvalid_dly : buffer std_logic;
      s00_axi_arready     : in     std_logic;
      s00_axi_rdata       : in     std_logic_vector(31 downto 0);
      s00_axi_rresp       : in     std_logic_vector(1 downto 0);
      s00_axi_rvalid      : in     std_logic;
      s00_axi_rready_dly  : buffer std_logic;

      -- Ports of Axi Master Bus Interface M00_AXIS -- FIFO-to-AXI Stream interface --
      m00_axis_aclk        : buffer std_logic;
      m00_axis_aresetn_dly : buffer std_logic;
      m00_axis_tvalid      : in     std_logic;
      m00_axis_tdata       : in     std_logic_vector(31 downto 0);
      m00_axis_tstrb       : in     std_logic_vector(3 downto 0);
      m00_axis_tlast       : in     std_logic;
      m00_axis_tready_dly  : buffer std_logic;

      -- Ports of Axi Master Bus Interface M01_AXIS -- Datamover command interface --
      m01_axis_aclk        : buffer std_logic;
      m01_axis_aresetn_dly : buffer std_logic;
      m01_axis_tvalid      : in     std_logic;
      m01_axis_tdata       : in     std_logic_vector(71 downto 0);
      m01_axis_tstrb       : in     std_logic_vector(8 downto 0);
      m01_axis_tlast       : in     std_logic;
      m01_axis_tready_dly  : buffer std_logic;

      -- Ports of Axi Slave Bus Interface S01_AXIS -- Datamover status interface --
      s01_axis_aclk        : buffer std_logic;
      s01_axis_aresetn_dly : buffer std_logic;
      s01_axis_tready      : in     std_logic;
      s01_axis_tdata_dly   : buffer std_logic_vector(7 downto 0);
      s01_axis_tstrb_dly   : buffer std_logic_vector(0 downto 0);
      s01_axis_tlast_dly   : buffer std_logic;
      s01_axis_tvalid_dly  : buffer std_logic);

end adc_spi_wrapper_v1_0_vgen;

-------------------------------------------------------------------------------
architecture vgen of adc_spi_wrapper_v1_0_vgen is

   ---------------------------------------------------------------------------
   --                 CONSTANT, TYPE AND GENERIC DEFINITIONS                --
   ---------------------------------------------------------------------------
   ---------------------------------------------------------------------------
   --  The INP_DELAY constants correspond to tester timing groups. Edit to
   --  change INP_DELAY values from the examples below. Delete the unused
   --  INP_DELAY constants.
   ---------------------------------------------------------------------------

   constant inp_delay : time := 3.0 ns;

   constant processor_clock_half_period : time := 10 ns;  -- 50MHz
   constant spi_clock_half_period       : time := 40 ns;  -- 12.5MHz

   constant c_num_devices   : integer                        := 1;
   constant c_adc_read_word : std_logic_vector(255 downto 0) := x"ae0123456789abcdef0123456789abcdae0123456789abcdef0123456789abcd";

   type t_vector24 is array (natural range <>) of unsigned(23 downto 0);

   ---------------------------------------------------------------------------
   --                          SIGNAL DECLARATIONS                          --
   ---------------------------------------------------------------------------
   signal clk_ps    : std_logic;
   signal clk_spi   : std_logic;
   signal axi_reset : std_logic := '0';

   signal spi0_mosi_o      : std_logic;
   signal spi0_ss_o        : std_logic;
   signal spi_miso         : std_logic;
   signal adc_drdy_n       : std_logic                    := '0';
   signal s00_axi_aresetn  : std_logic;
   signal s00_axi_awaddr   : std_logic_vector(5 downto 0);
   signal s00_axi_awprot   : std_logic_vector(2 downto 0);
   signal s00_axi_awvalid  : std_logic;
   signal s00_axi_wdata    : std_logic_vector(31 downto 0);
   signal s00_axi_wstrb    : std_logic_vector(3 downto 0);
   signal s00_axi_wvalid   : std_logic;
   signal s00_axi_bready   : std_logic;
   signal s00_axi_araddr   : std_logic_vector(5 downto 0);
   signal s00_axi_arprot   : std_logic_vector(2 downto 0);
   signal s00_axi_arvalid  : std_logic;
   signal s00_axi_rready   : std_logic;
   signal m00_axis_aresetn : std_logic;
   signal m00_axis_tready  : std_logic;
   signal m01_axis_aresetn : std_logic;
   signal m01_axis_tready  : std_logic;
   signal s01_axis_aresetn : std_logic;
   signal s01_axis_tdata   : std_logic_vector(7 downto 0);
   signal s01_axis_tstrb   : std_logic_vector(0 downto 0) := (others => '0');
   signal s01_axis_tlast   : std_logic;
   signal s01_axis_tvalid  : std_logic;

   -- local signals
   signal l_trig_reg_read     : std_logic                     := '0';
   signal l_trig_reg_read_d1  : std_logic                     := '0';
   signal l_trig_reg_write    : std_logic                     := '0';
   signal l_trig_reg_write_d1 : std_logic                     := '0';
   signal l_s00_axi_araddr    : std_logic_vector(5 downto 0)  := (others => '0');
   signal l_s00_axi_rdata     : std_logic_vector(31 downto 0) := (others => '0');
   signal l_s00_axi_rresp     : std_logic_vector(1 downto 0)  := b"00";
   signal l_s00_axi_awaddr    : std_logic_vector(5 downto 0)  := (others => '0');
   signal l_s00_axi_wdata     : std_logic_vector(31 downto 0) := (others => '0');
   signal l_s00_axi_bresp     : std_logic_vector(1 downto 0)  := b"00";

   signal l_adc_spi_cs_reg     : std_logic                                     := '0';
   signal l_adc_spi_cs_d1      : std_logic                                     := '0';
   signal l_adc_spi_cs_rising  : std_logic;
   signal l_adc_spi_cs_falling : std_logic;
   signal l_adc_spi_sclk_reg   : std_logic                                     := '1';
   signal l_adc_spi_sclk_d1    : std_logic                                     := '1';
   signal l_adc_spi_dout_reg   : std_logic                                     := '0';
   signal l_adc_spi_rw_latch   : std_logic                                     := '0';
   signal l_adc_rw_bit_latched : std_logic                                     := '0';
   signal l_adc_spi_bit_cnt    : unsigned(7 downto 0)                          := "00000000";
   signal l_adc_read_word      : std_logic_vector(96*c_num_devices-1 downto 0) := (others => '0');

   signal l_adc_samples : t_vector24(4*c_num_devices-1 downto 0) := (others => (others => '0'));



   ---------------------------------------------------------------------------
   --                        COMPONENT DECLARATIONS                         --
   ---------------------------------------------------------------------------


begin
   ---------------------------------------------------------------------------
   --                    INSTANTIATE COMPONENTS                             --
   ---------------------------------------------------------------------------

   ---------------------------------------------------------------------------
   --                    CONCURRENT SIGNAL ASSIGNMENTS                      --
   ---------------------------------------------------------------------------

   -- Assign AXI clocks
   s00_axi_aclk  <= clk_ps;  -- register interface --
   m00_axis_aclk <= clk_ps;  -- FIFO-to-AXI Stream interface --
   m01_axis_aclk <= clk_ps;  -- Datamover command interface --
   s01_axis_aclk <= clk_ps;  -- Datamover status interface --

   -- Assign AXI reset signals (active low)
   s00_axi_aresetn  <= not(axi_reset);
   m00_axis_aresetn <= not(axi_reset);
   m01_axis_aresetn <= not(axi_reset);
   s01_axis_aresetn <= not(axi_reset);

   ---------------------------------------------------------------------------
   --  Use the timing groups above to assign DUT input delays with respect to
   --  the clock. Use these to test for Setup/Hold timing violations.
   ---------------------------------------------------------------------------

   spi0_mosi_o_dly      <= spi0_mosi_o      after inp_delay;
   spi0_ss_o_dly        <= spi0_ss_o        after inp_delay;
   spi_miso_dly         <= spi_miso         after inp_delay;
   adc_drdy_n_dly       <= adc_drdy_n       after inp_delay;
   s00_axi_aresetn_dly  <= s00_axi_aresetn  after inp_delay;
   s00_axi_awaddr_dly   <= s00_axi_awaddr   after inp_delay;
   s00_axi_awprot_dly   <= s00_axi_awprot   after inp_delay;
   s00_axi_awvalid_dly  <= s00_axi_awvalid  after inp_delay;
   s00_axi_wdata_dly    <= s00_axi_wdata    after inp_delay;
   s00_axi_wstrb_dly    <= s00_axi_wstrb    after inp_delay;
   s00_axi_wvalid_dly   <= s00_axi_wvalid   after inp_delay;
   s00_axi_bready_dly   <= s00_axi_bready   after inp_delay;
   s00_axi_araddr_dly   <= s00_axi_araddr   after inp_delay;
   s00_axi_arprot_dly   <= s00_axi_arprot   after inp_delay;
   s00_axi_arvalid_dly  <= s00_axi_arvalid  after inp_delay;
   s00_axi_rready_dly   <= s00_axi_rready   after inp_delay;
   m00_axis_aresetn_dly <= m00_axis_aresetn after inp_delay;
   m00_axis_tready_dly  <= m00_axis_tready  after inp_delay;
   m01_axis_aresetn_dly <= m01_axis_aresetn after inp_delay;
   m01_axis_tready_dly  <= m01_axis_tready  after inp_delay;
   s01_axis_aresetn_dly <= s01_axis_aresetn after inp_delay;
   s01_axis_tdata_dly   <= s01_axis_tdata   after inp_delay;
   s01_axis_tstrb_dly   <= s01_axis_tstrb   after inp_delay;
   s01_axis_tlast_dly   <= s01_axis_tlast   after inp_delay;
   s01_axis_tvalid_dly  <= s01_axis_tvalid  after inp_delay;


   l_adc_spi_cs_rising  <= l_adc_spi_cs_reg and not(l_adc_spi_cs_d1);
   l_adc_spi_cs_falling <= not(l_adc_spi_cs_reg) and l_adc_spi_cs_d1;

   ---------------------------------------------------------------------------
   --                         CONCURRENT PROCESSES                          --
   ---------------------------------------------------------------------------


   ---------------------------------------------------------------------------
   --  Process:       PS_CLOCK_GEN 
   --  Timing Domain: Processor
   --  Purpose:       Generates Clock Waveform.
   --  Outputs:       CLK_PS
   ---------------------------------------------------------------------------
   ps_clock_gen : process
   begin
      clk_ps <= '0';
      --  Wait for the initial start up time.
      --  (You can edit the times below; Constants maybe used.)
      wait for processor_clock_half_period;

      endless : loop
         clk_ps <= '1';
         wait for processor_clock_half_period;
         clk_ps <= '0';
         wait for processor_clock_half_period;
      end loop;

   end process ps_clock_gen;

   ---------------------------------------------------------------------------
   --  Process:       SPI_CLOCK_GEN 
   --  Timing Domain: SPI
   --  Purpose:       Generates Clock Waveform.
   --  Outputs:       CLK_SPI
   ---------------------------------------------------------------------------
   spi_clock_gen : process
   begin
      clk_spi <= '0';
      --  Wait for the initial start up time.
      --  (You can edit the times below; Constants maybe used.)
      wait for spi_clock_half_period;

      endless : loop
         clk_spi <= '1';
         wait for spi_clock_half_period;
         clk_spi <= '0';
         wait for spi_clock_half_period;
      end loop;

   end process spi_clock_gen;

   ---------------------------------------------------------------------------
   --  Process:       AXI_REG_READ_PROC
   ---------------------------------------------------------------------------
   axi_reg_read_proc : process(clk_ps)
   begin
      if rising_edge(clk_ps) then
         l_trig_reg_read_d1 <= l_trig_reg_read;
         -- initiate the read cycle
         if ((l_trig_reg_read = '1')and (l_trig_reg_read_d1 = '0')) then
            s00_axi_araddr  <= l_s00_axi_araddr;
            s00_axi_arvalid <= '1';
         end if;
         -- de-assert the address valid signal and asser the read ready signal
         if (s00_axi_arready = '1') then
            s00_axi_arvalid <= '0';
            s00_axi_rready  <= '1';
         end if;
         -- de-assert the read ready signal and capture the data
         if (s00_axi_rvalid = '1') then
            s00_axi_rready  <= '0';
            l_s00_axi_rdata <= s00_axi_rdata;
            l_s00_axi_rresp <= s00_axi_rresp;
         end if;
      end if;  -- if rising_edge(clk_ps)
   end process axi_reg_read_proc;

   ---------------------------------------------------------------------------
   --  Process:       AXI_REG_WRITE_PROC
   ---------------------------------------------------------------------------
   axi_reg_write_proc : process(clk_ps)
   begin
      if rising_edge(clk_ps) then
         l_trig_reg_write_d1 <= l_trig_reg_write;
         -- initiate the write cycle
         if ((l_trig_reg_write = '1') and (l_trig_reg_write_d1 = '0')) then
            s00_axi_awaddr  <= l_s00_axi_awaddr;
            s00_axi_wdata   <= l_s00_axi_wdata;
            s00_axi_awvalid <= '1';
            s00_axi_wvalid  <= '1';
            s00_axi_bready  <= '1';
         end if;
         -- de-assert the write address valid signal
         if (s00_axi_awready = '1') then
            s00_axi_awvalid <= '0';
         end if;
         -- de-assert the write data valid signal
         if (s00_axi_wready = '1') then
            s00_axi_wvalid <= '0';
         end if;
         -- de-assert the write response ready signal
         if (s00_axi_bvalid = '1') then
            s00_axi_bready  <= '0';
            l_s00_axi_bresp <= s00_axi_bresp;
         end if;
      end if;  -- if rising_edge(clk_ps)
   end process axi_reg_write_proc;

   ---------------------------------------------------------------------------
   --  Process:       ADC_SAMPLE_READ_PROC
   ---------------------------------------------------------------------------
   adc_sample_read_proc : process(clk_ps)
      variable v_samp_frame_cnt : integer := 0;
   begin
      if rising_edge(clk_ps) then

         -- register the spi inputs from the host
         l_adc_spi_cs_reg   <= spi_ss;
         l_adc_spi_cs_d1    <= l_adc_spi_cs_reg;
         l_adc_spi_sclk_reg <= spi_sclk;
         l_adc_spi_sclk_d1  <= l_adc_spi_sclk_reg;
         l_adc_spi_dout_reg <= spi_mosi;

         -- reset the bit count on a falling edge of chip select
         if ((l_adc_spi_cs_reg = '0') and (l_adc_spi_cs_d1 = '1')) then
            v_samp_frame_cnt                                                  := v_samp_frame_cnt + 1;
            l_adc_spi_bit_cnt                                                 <= "00000000";
            l_adc_read_word((96*c_num_devices)-1 downto 96*(c_num_devices-1)) <= std_logic_vector(l_adc_samples(0)) &
                                                                                 std_logic_vector(l_adc_samples(1)) &
                                                                                 std_logic_vector(l_adc_samples(2)) &
                                                                                 std_logic_vector(l_adc_samples(3));
            for i in c_num_devices-1 downto 1 loop
               l_adc_read_word((96*i)-1 downto 96*(i-1)) <= std_logic_vector(l_adc_samples(4*i + 1)) &
                                                            std_logic_vector(l_adc_samples(4*i + 2)) &
                                                            std_logic_vector(l_adc_samples(4*i + 3)) &
                                                            std_logic_vector(l_adc_samples(4*i + 4));
            end loop;
            for i in 0 to c_num_devices-1 loop
               l_adc_samples(4*i)   <= to_unsigned((4*i) + 4*(v_samp_frame_cnt-1)+1, 24);
               l_adc_samples(4*i+1) <= to_unsigned((4*i) + 4*(v_samp_frame_cnt-1)+2, 24);
               l_adc_samples(4*i+2) <= to_unsigned((4*i) + 4*(v_samp_frame_cnt-1)+3, 24);
               l_adc_samples(4*i+3) <= to_unsigned((4*i) + 4*(v_samp_frame_cnt-1)+4, 24);
            end loop;
         end if;

         -- decode the R/W bit from the first bit of the transaction
         if (l_adc_spi_cs_reg = '0') then
            -- detect falling edge on the SPI clock
            if (((l_adc_spi_sclk_reg = '0') and (l_adc_spi_sclk_d1 = '1')) and (l_adc_rw_bit_latched = '0')) then
               l_adc_spi_rw_latch   <= l_adc_spi_dout_reg;
               l_adc_rw_bit_latched <= '1';
            end if;
         end if;

         -- reset the R/W bit and bit latched on the rising edge of the chip select
         if ((l_adc_spi_cs_reg = '1') and (l_adc_spi_cs_d1 = '0')) then
            l_adc_spi_rw_latch   <= '0';
            l_adc_rw_bit_latched <= '0';
         end if;


         -- detect the rising edge of the SPI clock
         if (l_adc_spi_cs_reg = '0') and ((spi_sclk = '1') and (l_adc_spi_sclk_reg = '0') and (l_adc_spi_rw_latch = '1')) then
            l_adc_spi_bit_cnt <= l_adc_spi_bit_cnt + 1;
            -- shift out ADC read data
            if (l_adc_spi_bit_cnt > 6) then
               spi_miso                                     <= l_adc_read_word(96*c_num_devices-1);
               l_adc_read_word(96*c_num_devices-1 downto 1) <= l_adc_read_word(96*c_num_devices-2 downto 0);
            end if;
         end if;

         -- simulate the ADC DRDY_N signal
         -- de-assert DRDY_N on the falling edge of the ADC SPI chip-select
         -- signal and re-assert on the rising edge
         if ((l_adc_spi_cs_reg = '0') and (l_adc_spi_cs_d1 = '1')) then
            adc_drdy_n <= '1';
         elsif ((l_adc_spi_cs_reg = '1') and (l_adc_spi_cs_d1 = '0')) then
            adc_drdy_n <= '0';
         else
            adc_drdy_n <= adc_drdy_n;
         end if;
         
      end if;  -- if rising_edge(clk_ps)
   end process adc_sample_read_proc;


   ---------------------------------------------------------------------------
   --  Process:  DRIVER
   --  Purpose:  To drive the output signals of the vector generator.
   --  Inputs:   Feedback from the DUT.
   --  Outputs:  Inputs to DUT.
   ---------------------------------------------------------------------------


   driver : process

   begin
      -----------------------------------------------------------------------
      --  Put your test vectors here!
      -----------------------------------------------------------------------
      -- initialise ADC sample values
      s00_axi_awprot <= "000";
      s00_axi_arprot <= "000";
      s00_axi_wstrb  <= "1111";  -- all bits valid in writes (no byte-enables)

      -- assert Datamover control interface tready signal
      m01_axis_tready <= '1';

      -- assert Datamove AXI stream interface tready signal
      m00_axis_tready <= '1';

      -- set the Datamover status word to a default "transaction OK" state
      s01_axis_tvalid <= '1';
      s01_axis_tdata  <= b"10000000";
      s01_axis_tlast  <= '1';

      wait until (clk_ps'event and clk_ps = '1');
      axi_reset <= '1';
      wait until (clk_ps'event and clk_ps = '1');
      wait until (clk_ps'event and clk_ps = '1');
      wait until (clk_ps'event and clk_ps = '1');
      wait until (clk_ps'event and clk_ps = '1');
      axi_reset <= '0';

      for i in 1 to 50 loop
         wait until (clk_ps'event and clk_ps = '1');
      end loop;

      -- execute a register read transaction
      l_s00_axi_araddr <= b"000000";
      l_trig_reg_read  <= '1';
      wait until (clk_ps'event and clk_ps = '1');
      l_trig_reg_read  <= '0';

      for i in 1 to 50 loop
         wait until (clk_ps'event and clk_ps = '1');
      end loop;

      -- execute a register write transaction
      l_s00_axi_awaddr <= b"000000";
      -- l_s00_axi_wdata  <= x"600dbeef";
      l_s00_axi_wdata  <= x"00000001";
      l_trig_reg_write <= '1';
      wait until (clk_ps'event and clk_ps = '1');
      l_trig_reg_write <= '0';

      for i in 1 to 10 loop
         wait until (clk_ps'event and clk_ps = '1');
      end loop;

      -- execute a register read transaction
      l_s00_axi_araddr <= b"000000";
      l_trig_reg_read  <= '1';
      wait until (clk_ps'event and clk_ps = '1');
      l_trig_reg_read  <= '0';

      for i in 1 to 10 loop
         wait until (clk_ps'event and clk_ps = '1');
      end loop;

      -- execute a register write transaction
--      l_s00_axi_wdata  <= x"00000000";
--      l_trig_reg_write <= '1';
--      wait until (clk_ps'event and clk_ps = '1');
--      l_trig_reg_write <= '0';


      endless : loop
         wait until (clk_ps'event and clk_ps = '1');
      end loop;


   end process driver;

end vgen;
